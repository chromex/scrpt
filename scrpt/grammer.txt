Language Features:
* Types: strings, numbers, lists, dictionaries, boolean
* Control structures: if, while, for, do, switch
* Operators: =, ==, <, >, <=, >=, *, /, +, -, *=, /=, +=, -=, !, !=
* Functions


all characters = ? all visible ansii characters ?;
letter = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" 
       | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" 
       | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "A"
       | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J"
       | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S"
       | "T" | "U" | "V" | "W" | "X" | "Y" | "Z";
terminal = '"', { all characters }, '"';

digit excluding zero = "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9";
digit = "0" | digit excluding zero;
natural number = digit excluding zero, { digit };
number = "0" | [ "-" ], natural number, [ ".", digit, { digit } ] | [ "-" ], "0", ".", digit, { digit };

bool = "true" | "false";

constant = terminal | number | bool | list | dict;

ident = ( letter | "_" ), { letter, digit, "_" };

key value pair = terminal, ":", expression;
dict = "{", [ key value pair, { ",", key value pair }, [ "," ] ], "}";
list = "[", [ expression, { ",", expression }, [ "," ] ], "]";
indexing = expression, "[", [ expression ], [ ":", [ expression ] ], "]";

block = "{", { statement }, "}";

do loop = "do", block, "while", "(", expression, ")", ";"
while loop = "while", "(", expression, ")", statement;
for loop = "for", "(", expression, ";", expression, ";", expression, ")", statement;
if = "if", "(", expression, ")", statement;
case = ( "case", constant | "default" ), ":", { statement }, [ "break", ";" ];
switch = "switch", "(", expression, ")", "{", { case }, "}";

assignment = ident, "=", expression;
call = ident, [ "(", [ expression, { ",", expression } ], ")" ];
compare = expression, ( "==" | "!=" | "<" | ">" | "<=" | ">=" ), expression;
return = "return", [ expression ], ";";

math = expression, ( "*" | "/" | "+" | "-" ), expression;
self math = ident, ( "*=" | "/=" | "+=" | "-=" ), expression;
invert = "!", expression;

parens = "(", expression, ")";
ident items = ident | call | assignment | self math;

expression = constant | ident items | invert | parens | compare | math | indexing;
statement = [ expression ], ";" | block | do loop | while loop | for loop | if | switch | return;

func = "func", ident, "(", [ ident, { ",", ident } ], ")", block;
program = func, { func };

// Expression led things: indexing, math, compare
// Indexing can be fixed by updating the LHS to limit types that can fill that role and then satisfy it w/ look ahead
// Math can be fixed with: http://www.gamedev.net/topic/416784-recursive-descent-parsing-handling-left-associativity/
// Compare :/