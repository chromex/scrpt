all_characters = ? all visible ansii characters ?;
letter = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" 
      | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" 
      | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "A"
      | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J"
      | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S"
      | "T" | "U" | "V" | "W" | "X" | "Y" | "Z";
terminal = '"', { all characters }, '"';

digit_excluding_zero = "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9";
digit = "0" | digit_excluding_zero;
natural_number = digit_excluding_zero, { digit };
number = "0" | [ "-" ], natural_number, [ ".", digit, { digit } ] | [ "-" ], "0", ".", digit, { digit };

bool = "true" | "false";

constant = terminal | number | bool | list | dict;

ident = ( letter | "_" ), { letter, digit, "_" }, [ index ], [ ( "--" | "++" ) ];

prefix = ( "--" | "++" ), ident;

key_value_pair = terminal, ":", expression;
dict = "{", [ key_value_pair, { ",", key_value_pair }, [ "," ] ], "}", [ index ];
list = "[", [ expression, { ",", expression }, [ "," ] ], "]", [ index ];
index = "[", [ expression ], [ ":", [ expression ] ], "]", { "[", [ expression ], [ ":", [ expression ] ], "]" };

*block = "{", { statement }, "}";

*do_loop = "do", block, "while", "(", expression, ")", ";"
*while_loop = "while", "(", expression, ")", statement;
*for_loop = "for", "(", expression, ";", expression, ";", expression, ")", statement;
*if = "if", "(", expression, ")", statement, { "elif", "(", expression, ")", statement }, [ "else", statement ];
*case = ( "case", constant | "default" ), ":", { statement };
*switch = "switch", "(", expression, ")", "{", { case }, "}";

assignment = ident, ( "=" | "*=" | "/=" | "+=" | "-=" | "%=" ), expression; 
call = ident, "(", [ expression, { ",", expression } ], ")", [ index ];
*return = "return", [ expression ], ";";
*break = "break", ";";

invert = "!", expression;
parens = "(", expression, ")", [ index ];
ident_items = ident | call | assignment;

term = constant | ident_items | invert | parens | prefix;
ex5 = term, ( "*" | "/" | "%" ), ex5 | term;
ex4 = ex5, ( "+" | "-" ), ex4 | ex5;
ex3 = ex4, ( "<" | ">" | "<=" | ">=" ), ex3 | ex4;
ex2 = ex3, ( "==" | "!=" ), ex2 | ex3;
ex1 = ex2, "&&", ex1 | ex2;
expression = ex1, "||", expression | ex1;

statement = [ expression ], ";" | block | do_loop | while_loop | for_loop | if | switch | return | break;

*func = "func", ident, "(", [ ident, { ",", ident } ], ")", block;
*program = { func };
